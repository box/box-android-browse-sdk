apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        group "com.box"


        // Set version code and name
        def env = System.getenv()
        def git_count = env['GIT_COUNT'] as Integer
        if (git_count != null) {
            versionCode git_count + 30000
            versionName "3.99." + git_count + "-SNAPSHOT"
            version "3.99." + git_count + "-SNAPSHOT"
        } else {
            versionCode 30002
            versionName "3.0.2"
            version "3.0.2"
        }
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize = "4g"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api 'com.box:box-android-sdk:5.99.725-SNAPSHOT'
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
}

apply from: '../maven_push.gradle'
